[[../../README.md | < Zur Übersicht]]

## Überprüfe, wie und wo die Sensordaten am sinnvollsten für das entwickelte Datenmodell geschickt werden müssen (JSON-Struktur / Wie viele bzw. welche Topics?) und ändere den Code auf dem ESP entsprechend.

### Zu den Topics
Um besser abzubilden, dass es nur die allgemeine, nicht Sensortyp-spezifische Entität `Measurement` gibt, die als Attribut einen `Sensortype` referenziert, will ich alle Daten auf ein Topic schicken – und nicht mehr wie bisher auf typ-spezifische Subtopics.

### JSON-Struktur
```
{
    "mac": <ESP-Mac-Adresse>,
    "type": <vordefinierte Typenbezeichnungen>,
    "value": <Messwert>
}
```

- Timestamp weiterhin erst bei Speicherung, da keine großartigen Delays zu erwarten sind.
- Wenn ein Sensortyp angegeben wird, der nicht vordefiniert ist, soll als `type` "unknown" geschickt werden, damit das System weiß, dass ein unbekannter Sensor angeschlossen ist.

### ESP-Code `sensorcode.ino`

```
#include <ESP8266WiFi.h>
#include <PubSubClient.h>
#include <DHT.h>
#include <WiFiManager.h>
#include "config.h"
#include <ArduinoJson.h>

#define DHTPIN D4
#define DHTTYPE DHT11
DHT dht(DHTPIN, DHTTYPE);
WiFiClient espClient;
PubSubClient mqttClient(espClient);

unsigned long lastTemperatureMeasure = 0;
unsigned long lastHumidityMeasure = 0;

enum SensorType
{
  TEMPERATURE,
  HUMIDITY
};

void setup()
{
  Serial.begin(115200);
  dht.begin();

  WiFiManager wifiManager;
  // wifiManager.resetSettings();

  if (!wifiManager.autoConnect(WIFIMANAGER_SSID))
  {
    Serial.println("Failed to connect and hit timeout");
    ESP.reset();
    delay(1000);
  }
  Serial.println("Connected to WiFi!");
  Serial.println("Mac Address: " + WiFi.macAddress());

  mqttClient.setServer(MQTT_SERVER, MQTT_PORT);
}


const char *sensorTypeToString(SensorType type)
{
  switch (type)
  {
  case TEMPERATURE:
    return "temperature";
  case HUMIDITY:
    return "humidity";
  default:
    return "unknown";
  }
}

void sendData(float value, SensorType sensorType)
{
  StaticJsonDocument<200> doc;
  doc["value"] = value;
  doc["type"] = sensorTypeToString(sensorType); // Setze den Sensortyp als String
  doc["mac"] = WiFi.macAddress();

  char jsonBuffer[512];
  serializeJson(doc, jsonBuffer);
  mqttClient.publish(MQTT_TOPIC, jsonBuffer); // Einheitliches Topic für alle Sensordaten
}

void loop()
{
  if (!mqttClient.connected())
  {
    reconnect();
  }
  mqttClient.loop();

  unsigned long currentMillis = millis();

  float humidity = dht.readHumidity();
  float temperature = dht.readTemperature();
  if (currentMillis - lastHumidityMeasure >= HUMIDITY_INTERVAL)
  {
    lastHumidityMeasure = currentMillis;
    if (!isnan(humidity))
    {
      sendData(humidity, HUMIDITY);
    }
  }
  if (currentMillis - lastTemperatureMeasure >= TEMPERATURE_INTERVAL)
  {
    lastTemperatureMeasure = currentMillis;
    if (!isnan(temperature))
    {
      sendData(temperature, TEMPERATURE);
    }
  }
  delay(100); // relieve cpu a bit
}

void reconnect()
{
  if (!mqttClient.connected())
  {

    String clientId = "ESP-" + WiFi.macAddress();
    if (mqttClient.connect(clientId.c_str()))
    {
      Serial.println("MQTT verbunden");
      String logTopic = String(MQTT_TOPIC) + "/logs";
      String logMessage = "ESP connected with Client ID: " + clientId;
      mqttClient.publish(logTopic.c_str(), logMessage.c_str());
    }
    else
    {
      Serial.print("Fehlgeschlagen, rc=");
      Serial.print(mqttClient.state());
      Serial.println(" Versuche es in 5 Sekunden erneut.");
    }
    delay(5000);
  }
}
```

#### Changelog:

- Eine `enum`, in der zulässige Sensortypen gelistet sind und eine Funktion, die diese in Strings für das Versenden in der JSON umwandelt
- Eine Funktion `sendData`, die für alle Sensoren genutzt wird und auf ein zentrales Topic publisht
- Die Möglichkeit, unterschiedliche Intervalle für die unterschiedlichen Sensortypen festzulegen.
- Logging auf dem Broker nach erfolgreicher Connection